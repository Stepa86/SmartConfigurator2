#Использовать WinExt

Перем СредаРазработки;
Перем ВыборИзСписка;
Перем Взаимодействие;

Перем РаботаСОкнами;

Процедура ПриСозданииОбъекта()
	
	СредаРазработки= ЗагрузитьСценарий("core\СредаРазработки.os");
	ВыборИзСписка = ЗагрузитьСценарий("core\SelectValue.os");
	Взаимодействие = ЗагрузитьСценарий("core\Взаимодействие.os");
	
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура ОбрамлениеТекста() Экспорт
	
	РаботаСОкнами = Новый РаботаСОкнами();
	РаботаСОкнами.ЗапомнитьТекущееОкно();

	Соответствие = Новый Соответствие();
	Соответствие.Вставить("Если", "Если");
	Соответствие.Вставить("Пока", "Пока");
	Соответствие.Вставить("Для", "Для");
	Соответствие.Вставить("Для каждого", "Для каждого");
	Соответствие.Вставить("Попытка", "Попытка");
	Соответствие.Вставить("Область", "Область");
	
	Результат = ВыборИзСписка.ВыбратьИзСписка(Соответствие);
	
	СтароеЗначениеБуфераОбмена = СредаРазработки.ПолучитьТекстИзБуфераОбмена();

	Если Результат <> Неопределено Тогда
		ВыполнитьОбрамлениеТекста(Результат);
	КонецЕсли;
	
	СредаРазработки.УстановитьТекстВБуферОбмена(СтароеЗначениеБуфераОбмена);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьОбрамлениеТекста(ВариантОбрамления)

	Отказ = Ложь;
	ВыделенныйТекст = СредаРазработки.ПолучитьВыделенныйТекст();
	ДобавитьОбрамлениеКТексту(ВыделенныйТекст, ВариантОбрамления, Отказ);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	СредаРазработки.УстановитьТекстВВыделение(ВыделенныйТекст);
	
КонецПроцедуры

Процедура ДобавитьОбрамлениеКТексту(Данные, Вариант, Отказ)
	
	Стр = Вариант;
	Если (Стр = "") Тогда
		Возврат;
	ИначеЕсли (Стр = "Если") Тогда
		Данные = "Если Тогда" + Символы.ПС + Данные + Символы.ПС + "КонецЕсли;";
	ИначеЕсли (Стр = "Пока") Тогда
		Данные = "Пока Цикл" + Символы.ПС + Данные + Символы.ПС + "КонецЦикла;";
	ИначеЕсли (Стр = "Для") Тогда
		Данные = "Для По Цикл" + Символы.ПС + Данные + Символы.ПС + "КонецЦикла;";
	ИначеЕсли (Стр = "Для каждого") Тогда
		Данные = "Для каждого Стр из Коллекция Цикл" + Символы.ПС + Данные + Символы.ПС + "КонецЦикла;";
	ИначеЕсли (Стр = "Попытка") Тогда
		Данные = "Попытка" + Символы.ПС + Данные + Символы.ПС + "Исключение" + Символы.ПС + "КонецПопытки;";
	ИначеЕсли (Стр = "Область") Тогда
		ИмяОбласти = Взаимодействие.ВвестиСтроку();
		Если СокрЛП(ИмяОбласти) <> "" Тогда
			Данные = "#Область " + ИмяОбласти + Символы.ПС + Символы.ПС + Данные + Символы.ПС + "#КонецОбласти";
		Иначе
			Отказ = Истина;
		КонецЕсли;
	Иначе
		ВызватьИсключение "Не определено действие с выделением";
	КонецЕсли;

	Данные = Данные + Символы.ПС;
	
КонецПроцедуры

#КонецОбласти
