#Область ПрограммныйИнтерфейс

Процедура ВыполнитьСкрипт() Экспорт
	
	Действия = ДействияСкриптов();
	
	ВыборИзСписка = ЗагрузитьСценарий("core\SelectValue.os");
	Результат = ВыборИзСписка.ВыбратьИзСписка(Действия);
	
	Если Результат <> Неопределено Тогда
		
		Параметры = СтрРазделить(Результат, "::", Истина);
	
		Скрипт = Параметры[0];
		Команда = Параметры[2];
		ПараметрыКоманды = Неопределено;

		ПутьКСкрипту = "scripts/" + Скрипт + ".os";
		ИсполняющийСкрипт = ЗагрузитьСценарий(ПутьКСкрипту);
		
		Рефлектор = Новый Рефлектор();
		Рефлектор.ВызватьМетод(ИсполняющийСкрипт, Команда);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДействияСкриптов()
	
	Скрипты = НайтиФайлы("scripts", "*.os", Истина);

	Список = Новый Соответствие();
	
	Для Каждого Скрипт Из Скрипты Цикл

		Анализатор = ЗагрузитьСценарий(Скрипт.ПолноеИмя);
		Рефлектор = Новый Рефлектор();
		МетодыСкрипта = Рефлектор.ПолучитьТаблицуМетодов(Анализатор);

		Путь = Скрипт.ПолноеИмя;
		Путь = СтрЗаменить(Путь, ТекущийКаталог() + "\scripts\", "");
		Путь = СтрЗаменить(Путь, Скрипт.Имя, "");
		
		ПутьКСкриптуБезРасширения = Путь + Скрипт.ИмяБезРасширения;

		Для Каждого Метод Из МетодыСкрипта Цикл

			Если Метод.КоличествоПараметров > 0 Тогда
				Продолжить;
			КонецЕсли;

			Если Метод.Экспорт = Истина Тогда
				Действие = ПутьКСкриптуБезРасширения + "::" + Метод.Имя;
				Список.Вставить(Действие, Действие);
			КонецЕсли;

		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Список;
	
КонецФункции

#КонецОбласти