Функция СтруктураЗаписи() Экспорт
	
	Возврат Новый Структура("Id, Key, Ctrl, Alt, Shift, Win, Command, App, Comment");
	
КонецФункции

Функция НайтиЭлемент(УИД) Экспорт
	
	Коллекция = Коллекция();
	
	Для каждого Запись Из Коллекция Цикл
		Если Запись.Id = УИД Тогда
			Возврат Запись;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция СохранитьЭлемент(Данные) Экспорт
	
	Коллекция = Коллекция();
	
	Если Данные.Id = Неопределено Тогда
		Данные.Вставить("Id", Строка(Новый УникальныйИдентификатор()));
		Коллекция.Добавить(Данные);
	Иначе
		Для каждого Запись Из Коллекция Цикл
			Если Запись.Id = Данные.Id Тогда
				
				СтруктураЗаписи = СтруктураЗаписи();
				Для каждого КлючЗначение Из СтруктураЗаписи Цикл
					
					Запись.Вставить(КлючЗначение.Ключ, Данные[КлючЗначение.Ключ]);
					
				КонецЦикла;

				Прервать;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СохранитьКоллекцию(Коллекция);
	
	Возврат Запись;
	
КонецФункции

Процедура УдалитьЭлемент(УИД) Экспорт
	Коллекция = Коллекция();
	
	Индекс = 0;
	Для каждого Запись Из Коллекция Цикл
		Если Запись.Id = УИД Тогда
			Прервать;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Коллекция.Удалить(Индекс);
	СохранитьКоллекцию(Коллекция);
	
КонецПроцедуры

Функция Коллекция() Экспорт
	
	Чтение = Новый ЧтениеJSON();
	Чтение.ОткрытьФайл("settings/hotkeys.json", КодировкаТекста.UTF8);
	
	Результат = Новый Массив;
	Пока Чтение.Прочитать() Цикл
		Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Тогда
			ВложеннаяСтруктура = Новый Структура;
			Результат.Добавить(ВложеннаяСтруктура);
			Пока Чтение.Прочитать() Цикл
				Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.КонецОбъекта Тогда
					Прервать;
				КонецЕсли;
				
				Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
					ИмяВложенногоСвойства = Чтение.ТекущееЗначение;
					Чтение.Прочитать();
					
					Если Чтение.ТипТекущегоЗначения = ТипЗначенияJSON.Число Тогда
						
						ВложеннаяСтруктура.Вставить(ИмяВложенногоСвойства, Формат(Чтение.ТекущееЗначение, "ЧГ="));
						
					ИначеЕсли (Чтение.ТипТекущегоЗначения <> ТипЗначенияJSON.НачалоМассива)
						И (Чтение.ТипТекущегоЗначения <> ТипЗначенияJSON.НачалоОбъекта) Тогда
						
						ВложеннаяСтруктура.Вставить(ИмяВложенногоСвойства, Чтение.ТекущееЗначение);
					Иначе
						
						ВызватьИсключение "Неопределенный тип данных";
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	Чтение.Закрыть();
	
	ОсвободитьОбъект(Чтение);
	
	Возврат Результат;
КонецФункции

Процедура СохранитьКоллекцию(МассивХоткеев) Экспорт
	
	Запись = Новый ЗаписьJson();
	Запись.ОткрытьФайл("settings/hotkeys.json");
	ЗаписатьJSON(Запись, МассивХоткеев);
	Запись.Закрыть();
	ОсвободитьОбъект(Запись);
	
КонецПроцедуры

Процедура ЗаписатьJSON(Запись, Значение)
	
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Запись.ЗаписатьНачалоМассива();
		Для Каждого Элемент Из Значение Цикл
			Запись.ЗаписатьНачалоОбъекта();
			Для Каждого КлючЗначение Из Элемент Цикл
				Запись.ЗаписатьИмяСвойства(КлючЗначение.Ключ);
				Запись.ЗаписатьЗначение(КлючЗначение.Значение);
			КонецЦикла;
			Запись.ЗаписатьКонецОбъекта();
		КонецЦикла;
		Запись.ЗаписатьКонецМассива();
	КонецЕсли;
	
КонецПроцедуры

