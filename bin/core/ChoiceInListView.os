#использовать gui
#использовать WinExt

Перем УправляемыйИнтерфейс;
Перем ЭтаФорма;
Перем КолонкаНомераСтроки;
Перем РезультатВыбора Экспорт;

Перем ПолеПоиска;
Перем ПолеСписка;
Перем ДанныеСписка;
Перем ФильтрПоПолю;
Перем ЗаголовокОкнаВыбора;

Перем ДополнительныеПараметры;

Процедура ПриСозданииОбъекта()
	РезультатВыбора = Неопределено;
КонецПроцедуры

Процедура Предупреждение(ТекстПредупреждения)
	УправляемыйИнтерфейс.СтандартныеДиалоги.Предупреждение(ТекстПредупреждения);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработка событий
//
Процедура ПриВыбореСкрипта() Экспорт

	РезультатВыбора = ПолеСписка.ВыделеннаяСтрока[КолонкаНомераСтроки];
	ЭтаФорма.Закрыть();

	Если ДополнительныеПараметры <> Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("РаботаСОкнами") Тогда
			ДополнительныеПараметры.РаботаСОкнами.АктивироватьЗапомненноеОкно();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриИзменииПолеПоиска() Экспорт
	
	Попытка
		
		СтрПоиска = СтрЗаменить(ПолеПоиска.Значение, " ", "(.*)");
		
		РегВыражениеПоиск = Новый РегулярноеВыражение("(.*)(" + СтрПоиска + ")(.*)");
		РегВыражениеПоиск.ИгнорироватьРегистр = Истина;
		
		СтрокиСписка = ДанныеСписка.Источник.Скопировать();

		НовыеДанные = СтрокиСписка.СкопироватьКолонки();

		Для Каждого СтрСписка Из СтрокиСписка Цикл
			
			Попытка
				АнализируемоеЗначение = СтрСписка[ФильтрПоПолю];
				Совпадения = РегВыражениеПоиск.НайтиСовпадения(АнализируемоеЗначение);	
			Исключение
				Совпадения = Новый Массив();
			КонецПопытки;
			
			Если Совпадения.Количество() > 0 Тогда
				СтрокаНовыхДанных = НовыеДанные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаНовыхДанных, СтрСписка);
			КонецЕсли;
			
		КонецЦикла;
		
		Если НовыеДанные.Количество() = 0 Тогда

			Взаимодействие = ЗагрузитьСценарий("core/Взаимодействие.os");
			Взаимодействие.УведомитьЧерезТрей("Нет данных для выбора. Отображаем пустой список!", "Выбор из списка");

		КонецЕсли;

		Если СтрПоиска = "" Тогда
			НовыеДанные = СтрокиСписка;
		КонецЕсли;

		НовыйПровайдер = Новый Провайдер;
		НовыйПровайдер.Источник = НовыеДанные;

		ПолеСписка.ПутьКДанным = НовыйПровайдер;
		
		Если ЗаголовокОкнаВыбора = "" Тогда
			ЭтаФорма.Заголовок = "Количество элементов: " + НовыеДанные.Количество();
		Иначе
			ЭтаФорма.Заголовок = ЗаголовокОкнаВыбора;
		КонецЕсли;
		
	Исключение
		
		Сообщить(ОписаниеОшибки());

	КонецПопытки;
	
КонецПроцедуры

Процедура ПолеСпискаПриНажатииНаКлавишу() Экспорт

	Если ПолеСписка.НажатШифт Тогда
		
		Если ПолеСписка.КодНажатойКлавиши = 40 Тогда
			ПолеПоиска.Значение = ПолучитьКлючПоЗначению(ПолеСписка.СписокВыбора, ПолеСписка.Значение);
		КонецЕсли;
		
		Если ПолеСписка.КодНажатойКлавиши = 38 Тогда
			ПолеПоиска.Значение = ПолучитьКлючПоЗначению(ПолеСписка.СписокВыбора, ПолеСписка.Значение);
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПолеСписка.КодНажатойКлавиши = 37) или (ПолеСписка.КодНажатойКлавиши = 39) Тогда
		ЭтаФорма.ТекущийЭлемент = ПолеПоиска;
	КонецЕсли;
	
	Если ПолеСписка.КодНажатойКлавиши = 27 Тогда
		ПолеПоиска.Значение = "";
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеПоискаПриНажатииНаКлавишу() Экспорт
	
	Если ПолеПоиска.КодНажатойКлавиши = 27 Тогда
		ПолеПоиска.Значение = "";
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
	Если (ПолеПоиска.КодНажатойКлавиши = 38) ИЛИ (ПолеПоиска.КодНажатойКлавиши = 40) Тогда
		ЭтаФорма.ТекущийЭлемент = ПолеСписка;
	КонецЕсли;

	Если ПолеПоиска.КодНажатойКлавиши = 13 Тогда

		Если (ПолеПоиска.Значение = "") и (ПолеСписка.СписокВыбора.Количество() > 0) Тогда
			РезультатВыбора = ПолеСписка.ВыделеннаяСтрока[КолонкаНомераСтроки];
			ЭтаФорма.Закрыть();
		КонецЕсли;

		Если ПолеСписка.СписокВыбора.Количество() = 1 Тогда
			РезультатВыбора = ПолеСписка.ВыделеннаяСтрока[КолонкаНомераСтроки];
			ЭтаФорма.Закрыть();
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция ПолучитьКлючПоЗначению(Список, Значение)
	
	Для Каждого ЭлСписка Из Список Цикл
		Если ЭлСписка.Значение = Значение Тогда
			Возврат ЭлСписка.Ключ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

Функция ВыбратьИзСписка(Данные, ПолеДляФильтрации, ПолеНомераСтроки, ЗаголовокОкна = "", Параметры = Неопределено) Экспорт
	
	Попытка
		ДополнительныеПараметры = Параметры;
		
		ФильтрПоПолю = ПолеДляФильтрации;
		КолонкаНомераСтроки = ПолеНомераСтроки;
		ЗаголовокОкнаВыбора = ЗаголовокОкна;
		
		ДанныеСписка = Новый Провайдер;
		ДанныеСписка.Источник = Данные;

		УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
		ЭтаФорма = УправляемыйИнтерфейс.СоздатьФорму();
		
		ПолеПоиска                    	= ЭтаФорма.Элементы.Добавить("ПолеПоиска", "ПолеФормы", Неопределено);
		ПолеПоиска.Вид                	= ЭтаФорма.ВидПоляФормы.ПолеВвода;
		ПолеПоиска.ПоложениеЗаголовка	= ЭтаФорма.ПоложениеЗаголовка.Нет;
		
		ПолеПоиска.УстановитьДействие(ЭтотОбъект, "ПриИзменении", "ПриИзменииПолеПоиска");
		ПолеПоиска.УстановитьДействие(ЭтотОбъект, "ПриНажатииНаКлавишу", "ПолеПоискаПриНажатииНаКлавишу");
		
		ПолеСписка = ЭтаФорма.Элементы.Добавить("ПредставлениеСписка", "ПредставлениеСписка", Неопределено);
		ПолеСписка.ПоложениеЗаголовка          = ЭтаФорма.ПоложениеЗаголовка.Нет;
		ПолеСписка.КолонкаСИндексомИзображения = -1;
		ПолеСписка.ПутьКДанным                 = ДанныеСписка;
		ПолеСписка.ПоложениеЗаголовка          = УправляемыйИнтерфейс.ПоложениеЗаголовка.Верх;
		ПолеСписка.Закрепление                 = УправляемыйИнтерфейс.СтильЗакрепления.Заполнение;
		ПолеСписка.ВыделятьВсюСтроку           = Истина;
		ПолеСписка.БезИзображений();
		
		ПолеСписка.УстановитьДействие(ЭтотОбъект, "ПриВыборе", "ПриВыбореСкрипта");
		ПолеСписка.УстановитьДействие(ЭтотОбъект, "ПриДвойномКлике", "ПриВыбореСкрипта");
		ПолеСписка.УстановитьДействие(ЭтотОбъект, "ПриНажатииНаКлавишу", "ПолеСпискаПриНажатииНаКлавишу");
		
		Если ЗаголовокОкнаВыбора = "" Тогда
			ЭтаФорма.Заголовок = "Количество элементов: " + Данные.Количество();
		Иначе
			ЭтаФорма.Заголовок = ЗаголовокОкнаВыбора;
		КонецЕсли;
		
		ЭтаФорма.Ширина = 800;
		ЭтаФорма.ПоверхОкон = Истина;
		ЭтаФорма.СостояниеОкна = "Обычное";
		ЭтаФорма.СтартоваяПозиция = "ЦентрЭкрана";
		ЭтаФорма.Показать();
		
	Исключение
		Взаимодействие = ЗагрузитьСценарий("core/Взаимодействие.os");
		Взаимодействие.Предупреждение(ОписаниеОшибки(), "Выбор из списка");
	КонецПопытки;
	
	Возврат РезультатВыбора;

КонецФункции
